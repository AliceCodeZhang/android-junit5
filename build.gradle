buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$BINTRAY_PLUGIN_VERSION"
    }
}

// Populate deployment credentials:
// * Local development - stored in properties file
// * CI Server - stored in environment variables
def bintrayUser = ""
def bintrayKey = ""
def sonatypeUser = ""
def sonatypePass = ""

def credentialsFile = new File(project.rootDir, "local.properties")
if (credentialsFile.exists()) {
    Properties properties = new Properties()
    credentialsFile.withReader { properties.load(it) }
    bintrayUser = properties.get("BINTRAY_USER")
    bintrayKey = properties.get("BINTRAY_KEY")
    sonatypeUser = properties.get("SONATYPE_USER")
    sonatypePass = properties.get("SONATYPE_PASS")

} else {
    bintrayUser = System.getenv("bintrayUser")
    bintrayKey = System.getenv("bintrayKey")
    sonatypeUser = System.getenv("sonatypeUser")
    sonatypePass = System.getenv("sonatypePass")
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    // Store deployment credentials
    ext.bintrayUser = bintrayUser
    ext.bintrayKey = bintrayKey
    ext.sonatypeUser = sonatypeUser
    ext.sonatypePass = sonatypePass
}
