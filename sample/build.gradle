buildscript {
    ext.kotlinVersion = "1.1.4-3"

    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
        jcenter()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.0.0"
        classpath "com.android.tools.build:gradle:2.3.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "me.tatarka:gradle-retrolambda:3.7.0"
    }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "jacoco"

android {
    // !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    // A Note to my forgetful self:
    //
    // When updating these values, make sure
    // to always update the Travis CI .yml config
    // & the plugin's AndroidJUnitPlatformSpec, too
    // !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "de.mannodermaus.gradle.plugins.android_junit5.sample"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        test.java.srcDirs += "src/test/kotlin"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testCompile junit5()
    testCompile junit5Params()
    testProvided "de.mannodermaus.gradle.plugins:android-junit5-embedded-runtime:1.0.0-RC3-rev1"
}

// IJ doesn't recognize Kotlin test classes properly,
// so we copy them over manually

afterEvaluate {
    android.buildTypes.all {
        def name = it.name

        def copyTask = project.task(type: Copy, "copyKotlin${name.capitalize()}UnitTestClasses") {
            from "build/tmp/kotlin-classes/${name}UnitTest"
            into "build/intermediates/classes/test/$name"
        }

        project.tasks.getByName("compile${name.capitalize()}UnitTestSources").finalizedBy copyTask
    }
}
