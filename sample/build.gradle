buildscript {
    ext.kotlinVersion = "1.1.4-2"

    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
        jcenter()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath "de.mannodermaus.gradle.plugins:android-junit5:+"
        classpath "com.android.tools.build:gradle:$ANDROID_PLUGIN_VERSION_3X"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "de.mannodermaus.gradle.plugins.android_junit5.sample"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        test.java.srcDirs += "src/test/kotlin"
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testImplementation junitJupiter()
    testImplementation junitParams()
}

// IJ doesn't recognize Kotlin test classes properly,
// so we copy them over manually

afterEvaluate {
    android.buildTypes.all {
        def name = it.name

        def copyTask = project.task(type: Copy, "copyKotlin${name.capitalize()}UnitTestClasses") {
            from "build/tmp/kotlin-classes/${name}UnitTest"
            into "build/intermediates/classes/test/$name"
        }

        project.tasks.getByName("assemble${name.capitalize()}UnitTest").dependsOn copyTask
    }
}
